* Complementary CMOS Full Adder
************ transistor tech file ************
.include mosfet.txt
************ Define the input signal info. ********
.PARAM bitrate1=0.2G
  +freq1='bitrate1/2'
  +per1='1/freq1'
  +td1=0
  +tr1='per1*0.1'
  +tf1='per1*0.1'
  +pw1='per1*0.4'
.PARAM bitrate2=0.1G
  +freq2='bitrate2/2'
  +per2='1/freq2'
  +td2=0
  +tr2='per2*0.05'
  +tf2='per2*0.05'
  +pw2='per2*0.4'
.PARAM bitrate3=0.05G
  +freq3='bitrate3/2'
  +per3='1/freq3'
  +td3=0
  +tr3='per3*0.025'
  +tf3='per3*0.025'
  +pw3='per3*0.4'
Vin1 in1 0 pulse(0 3.3 td1 tr1 tf1 pw1 per1)
Vin2 in2 0 pulse(0 3.3 td2 tr2 tf2 pw2 per2)
Vin3 cin 0 pulse(0 3.3 td3 tr3 tf3 pw3 per3)
******* Define the Vdd and Vss voltage level ********
Vdd vdd 0 DC 3.3v
******Complementary CMOS Full Adder Circuits ******
X1 in1 in2 cin n1 vdd 0 a
X2 n1 cout vdd 0 inv1
X3 n1 in1 in2 cin n2 vdd 0 b
X4 n2 sum vdd 0 inv2
****************** Subcircuits ********************
.subckt a in1 in2 cin out vdd gnd
Mp1 out in1 n2 vdd PMOS l=0.5u w=20u
Mp2 out cin n3 vdd PMOS l=0.5u w=20u
Mp3 n2 in2 n3 vdd PMOS l=0.5u w=20u
Mp4 n3 in1 vdd vdd PMOS l=0.5u w=20u
Mp5 n3 in2 vdd vdd PMOS l=0.5u w=20u
Mn1 n1 in1 gnd gnd NMOS l=0.5u w=10u
Mn2 n1 in2 gnd gnd NMOS l=0.5u w=10u
Mn3 n4 in2 gnd gnd NMOS l=0.5u w=10u
Mn4 out cin n1 gnd NMOS l=0.5u w=10u
Mn5 out in1 n4 gnd NMOS l=0.5u w=10u

.ends

.subckt inv1 in out vdd gnd
Mp1 out in vdd vdd PMOS l=0.5u w=20u
Mn1 out in gnd gnd NMOS l=0.5u w=10u
.ends

.subckt b in in1 in2 cin out vdd gnd
Mp1 out in n11 vdd PMOS l=0.5u w=20u
Mp2 out cin n9 vdd PMOS l=0.5u w=20u
Mp3 n9 in2 n10 vdd PMOS l=0.5u w=20u
Mp4 n10 in1 n11 vdd PMOS l=0.5u w=20u
Mp5 n11 cin vdd vdd PMOS l=0.5u w=20u
Mp6 n11 in1 vdd vdd PMOS l=0.5u w=20u
Mp7 n11 in2 vdd vdd PMOS l=0.5u w=20u
Mn1 n6 in1 gnd gnd NMOS l=0.5u w=10u
Mn2 n6 in2 gnd gnd NMOS l=0.5u w=10u
Mn3 n6 cin gnd gnd NMOS l=0.5u w=10u
Mn4 out in n6 gnd NMOS l=0.5u w=10u
Mn5 n7 in2 gnd gnd NMOS l=0.5u w=10u
Mn6 n8 in1 n7 gnd NMOS l=0.5u w=10u
Mn7 out cin n8 gnd NMOS l=0.5u w=10u
.ends

.subckt inv2 in out vdd gnd
Mp1 out in vdd vdd PMOS l=0.5u w=20u
Mn1 out in gnd gnd NMOS l=0.5u w=10u
.ends

***** Define the resoltion and simulation time ****
.tran 1p 25n
************* Plot the results *****************
.control
set color0=white
run
plot	v(in1) v(in2) v(cin)
plot   v(cout) v(sum)
.endc
****************************************************
.end

